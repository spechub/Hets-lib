library SpatialReasoning/Modal_RCC8

from Basic/RelationsAndOrders get RichBooleanAlgebra
from Modal/BasicSystems get S4, S5

logic Modal

spec Modal_RCC8 =
  modality u { []p => p; []A => [][]A; <>A => []<>A }
and
  S4
and
  logic CASL : {RichBooleanAlgebra with Elem |-> Region} with logic CASL2Modal
then
  flexible pred Tr : Region
  forall x,y:Region
  . Tr(0) <=> false
  . Tr(1) <=> true
  . Tr(compl x) <=> [] not Tr(x)
  . Tr(x cap y) <=> [] <> (Tr(x) /\ Tr(y))
  . Tr(x cup y) <=> Tr(x) \/ Tr(y)

  rigid pred EQ,DC, EC,PO,TPP,NTPP : Region * Region
  forall x,y:Region
  . EQ(x,y) <=> [u] (Tr(x) <=> Tr(y))
  . DC(x,y) <=> not <u> (Tr(x) /\ Tr(y))
  . EC(x,y) <=> <u> (Tr(x) /\ Tr(y)) /\ not <u> ([]Tr(x) /\ []Tr(y))
  . PO(x,y) <=> <u> ([]Tr(x) /\ []Tr(y))
                /\ <u> (Tr(x) /\ not [] Tr(y))
                /\ <u> (not [] Tr(x) /\ Tr(y))
  . TPP(x,y) <=> [u](Tr(x) => Tr(y)) /\ <u> (Tr(x) /\ not [] Tr(y))
  . NTPP(x,y) <=> [u](Tr(x) => [] Tr(y))
end
