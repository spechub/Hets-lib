
library Calculi/Space/RCCDagstuhl
version 0.1

%% author: S. Wölfl, T. Mossakowski
%% date: 25-11-2005


%( Proving correctness of composition table:
	We start with Bennett's version of RCC_FirstOrder 
	from his PhD thesis (1997) 
)%

from HasCASL/MetricSpaces get 
	MetricSpace,
	%%ExtMetricSpaceByOpenDiscs,
	ExtMetricSpaceByClosedDiscs
	%%RichMetricSpace
%%from Calculi/Space/RCCDagstuhl get 
%%	RCC5BaseRelationModel_OpenDisc,
%%	RCC5Model_OpenDisc



spec RCC_FO = 
     sort QReg
     pred __C__: QReg * QReg;

     forall x,y: QReg
     . x C y => x C x					        %(C_non_null)%
     . x C y => y C x						%(C_sym)%
     . (forall z:QReg . z C x <=> z C y) => x = y     		%(C_id)%
     . exists x:QReg . x C x					%(C_non_triv)%

end

spec ExtRCC_FO =
     RCC_FO
then %def
     preds  __P__, __O__, __NTP__: QReg * QReg
     forall x,y: QReg
     . x P y   <=> forall z:QReg .(z C x => z C y)		%(P_def)%
     . x O y   <=> exists z:QReg .(z C z /\ z P x /\ z P y)	%(O_def)%
     . x NTP y <=> forall z:QReg .(z C x => z O y)		%(NTP_def)%
     sort Reg = { x: QReg . x C x }

then %implies
     forall x,y,z: Reg  
     . x O y => y O x						%(O_sym)%
     . x P y /\ y P z => x P z					%(cmps_PP)%
     . x P y => x C y						%(P_impl_C)%
     . x P y => x O y						%(P_impl_O)%
     . x O y => x C y						%(O_impl_C)%
     . x NTP y => x C y						%(NTP_impl_C)%
     . x NTP y => x P y						%(NTP_impl_P)%
     . x P y /\ x O z => y O z					%(P_inh_O)%
     . x P y /\ y P x => x = y					%(extens)%
     . x NTP y /\ y NTP z => x NTP z				%(NTP_trans)%
     . x NTP y /\ y P z => x NTP z				%(cmps_NTP_P)%
     . x P y /\ y NTP z => x NTP z				%(cmps_P_NTP)%
end




spec ExtRCCByFuncts[ExtRCC_FO] = %def
     ops cp: QReg * QReg -> QReg;				%% complement of product
	 compl: QReg -> QReg;
	 sum,prod: QReg * QReg -> QReg;
	 0,1:QReg

     forall x,y,z: QReg  
     . cp(x,y) = z <=> forall u:QReg . z C u <=> not u NTP x \/ not u NTP y	%(cp_def)% 
     . compl(x) = cp(x,x)					%(compl_def)%
     . sum(x,y) = cp(cp(x,x),cp(y,y))				%(sum_def)%
     . prod(x,y) = cp(cp(x,y),cp(x,y))				%(prod_def)%
     . 0 = prod(x,compl(x))					%(0_def)%
     . 1 = sum(x,compl(x))					%(1_def)%

then %implies
    forall x,y,z: QReg
    . cp(x,y) C z <=> not z NTP x \/ not z NTP y		%(cp_def_a)%
    . x P y <=> cp(x,y) = cp(x,x)				%(cp_P_a)%
    . x P y <=> cp(x,y) = cp(x,x)				%(cp_P_b)%
    . x P y => cp(z,y) P cp(x,y)				%(cp_P_c)%
    . cp(x,y) = z <=> cp(y,x) =z				%(cp_com_1)% 
    . cp(x,y) = cp(y,x)						%(cp_com)%
    . sum(x,x) = x						%(sum_idem)%
    . prod(x,x) = x						%(prod_idem)%
    . compl(0) = 1						%(0_compl)%
    . compl(1) = 0						%(1_compl)%
    . not 0 = 1							%(not_0_eq_1)%
    . not 0 C 0							%(0_th_non_C)%
    . 0 P x							%(0_P)%
    . not 0 in Reg						%(0_th_a)%
    . x = 0 <=> not x C x					%(0_th_b)%
    . x in Reg => not x P 0					%(0_th_c)%
    . x in Reg => not x C 0					%(0_th_c)%
    . 1 in Reg							%(1_th_a)%
    . x in Reg => x C 1						%(1_th_b)%
    . x P 1 							%(1_th_c)%
    . not x = 0 /\ not x = 1 => x C compl(x)			%(compl_th_a)%
    . not x = 0 /\ not x = 1 => x O compl(x)			%(compl_th_b)%
    . compl(x) = y <=> compl(y) = x				%(compl_th_c)%
    . compl(compl(x)) = x					%(compl_th_d)%
    . sum(x,y) = y <=> x P y					%(sum_th_a)%
    . sum(x,y) P z <=> x P z /\ y P z				%(sum_th_b)%
    . x P sum(x,y) 		 				%(sum_th_c)%
    . sum(x,y) = sum(y,x)					%(sum_th_d)%
    . prod(x,y) = x <=> x P y					%(prod_th_a)%
    . x P z \/ y P z => prod(x,y) P z 				%(prod_th_b)%
    . prod(x,y) P x		 				%(prod_th_c)%
    . prod(x,y) = prod(y,x)	 				%(prod_th_d)%
    
end


spec ExtRCCByRCC5Rels[ExtRCC_FO] = %def 
     preds
	  __PP__,  __PO__,  __EQ__, 
	  __DR__,  __PPi__, __?__: Reg * Reg

     forall x,y:Reg 
     . x PP y   <=>  x P y /\ not y P x              		%(PP_def)%
     . x PO y   <=>  x O y /\ not x P y /\ not y P x		%(PO_def)%
     . x DR y   <=>  not x O y					%(DR_def)%      	
     . x PPi y  <=> y PP x                                 	%(PPi_def)%
     . x EQ y   <=> x = y	                		%(EQ_def)%
     . x ? y    <=> x DR y \/ x PO y \/ x PP y \/ x PPi y \/ x EQ y 
								%(?_RCC5_def)%
then %implies
     forall x,y,z:Reg 
     . x ? y							%(?_RCC5)%

     . x DR y => y DR x						%(sym_DR)%
     . x PO y => y PO x						%(sym_PO)%
     . x EQ y => y EQ x						%(sym_EQ)%

     . x DR y => not x PO y /\ not x PP y /\ not x PPi y /\ not x EQ y  %(disj_DR)% 
     . x PO y => not x DR y /\ not x PP y /\ not x PPi y /\ not x EQ y  %(disj_PO)% 
     . x PP y => not x DR y /\ not x PO y /\ not x PPi y /\ not x EQ y 	%(disj_PP)% 
     . x PPi y => not x DR y /\ not x PO y /\ not x PP y /\ not x EQ y	%(disj_PPi)% 
     . x EQ y => not x DR y /\ not x PO y /\ not x PP y /\ not x PPi y 	%(disj_EQ)% 

     . x PP y /\ y PP z => x PP	z				%(cmps_PPPP)%	
     . x PP y /\ y PPi z => x ? z  				%(cmps_PPPPi)%
     . x PP y /\ y PO z => x PP z \/ x PO z \/ x DR z		%(cmps_PPPO)%
     . x PP y /\ y DR z => x DR z				%(cmps_PPDR)%
     . x PP y /\ y EQ z => x PP z				%(cmps_PPEQ)%

     . x PPi y /\ y PP z => not x DR z				%(cmps_PPiPP)%	
     . x PPi y /\ y PPi z => x ? z				%(cmps_PPiPPi)%
     . x PPi y /\ y PO z => x PPi z \/ x PO z			%(cmps_PPiPO)%
     . x PPi y /\ y DR z => x PPi z \/ x PO z \/ x DR z		%(cmps_PPiDR)%
     . x PPi y /\ y EQ z => x PPi z				%(cmps_PPiEQ)%

     . x PO y /\ y PP z => x PP z \/ x PO z			%(cmps_POPP)%	
     . x PO y /\ y PPi z => x PPi z \/ x PO z \/ x DR z		%(cmps_POPPi)%
     . x PO y /\ y PO z => x ? z      				%(cmps_POPO)%
     . x PO y /\ y DR z => x PPi z \/ x PO z \/ x DR z		%(cmps_PODR)%
     . x PO y /\ y EQ z => x PO z				%(cmps_POEQ)%

     . x DR y /\ y PP z => x PP z \/ x PO z \/ x DR z		%(cmps_DRPP)%	
     . x DR y /\ y PPi z => x DR z				%(cmps_DRPPi)%
     . x DR y /\ y PO z => x PP z \/ x PO z \/ x DR z		%(cmps_DRPO)%
     . x DR y /\ y DR z => x ? z				%(cmps_DRDR)%
     . x DR y /\ y EQ z => x DR	z 				%(cmps_DREQ)%

     . x EQ y /\ y PP z => x PP z   				%(cmps_EQPP)%	
     . x EQ y /\ y PPi z => x PPi z				%(cmps_EQPPi)%
     . x EQ y /\ y PO z => x PO	z				%(cmps_EQPO)%
     . x EQ y /\ y DR z => x DR	z 				%(cmps_EQDR)%
     . x EQ y /\ y EQ z => x EQ z				%(cmps_EQEQ)%

end


spec ExtRCCByRCC8Rels[ExtRCC_FO] = 	
     ExtRCCByRCC5Rels[ExtRCC_FO] with __?__ |-> __??__
then %def 
     preds
	  __EC__,  __DC__, __TPP__, __NTPP__, __TPPi__,  
		__NTPPi__, __?__: Reg * Reg

     forall x,y:Reg 
     .   x EC y   <=>  x C y /\ not x O y	          	%(EC_def)%
     .   x DC y   <=>  not x C y				%(DC_def)%   
     .  x TPP y   <=>  x PP y /\ exists z:Reg. z EC x /\ z EC y %(TPP_def)%
     . x NTPP y   <=>  x PP y /\ forall z:Reg. z EC x => not z EC y 
								%(NTPP_def)%
     .  x TPPi y  <=> y TPP x                                	%(TPPi_def)%
     . x NTPPi y  <=> y NTPP x	                		%(NTPPi_def)%
     . x ? y    <=> x DC y \/ x EC y \/ x PO y \/ x TPP y \/ 
		 x NTPP y \/ x TPPi y \/ x NTPPi y \/ x EQ y    %(?_RCC8_def)%

then %implies
     forall x,y,z:Reg 

     . x DR y <=> x DC y \/ x EC y				%(DR_eq_DC_or_EC)%
     . x PP y <=> x TPP y \/ x NTPP y				%(PP_eq_TPP_or_NTPP)%
     . x PPi y <=> x TPPi y \/ x NTPPi y			%(PPi_eq_TPPi_or_NTPPi)%

     . x ? y 							%(?_RCC8)%
     . x ? y <=> x ?? y						%(?_RCC5_RCC8)%

     . x DC y => y DC x						%(sym_DC)%
     . x EC y => y EC x						%(sym_EC)%

     . x DC y => not x EC y /\ not x PO y /\ not x TPP y /\ not x NTPP y 
		/\ not x TPPi y /\ not x NTPPi y /\ not x EQ y  			%(disj_DC)% 
     . x EC y => not x DC y /\ not x PO y /\ not x TPP y /\ not x NTPP y 
		/\ not x TPPi y /\ not x NTPPi y /\ not x EQ y  			%(disj_EC)% 
     . x PO y => not x DC y /\ not x EC y /\ not x TPP y /\ not x NTPP y 
		/\ not x TPPi y /\ not x NTPPi y /\ not x EQ y  			%(disj_PO)% 
     . x TPP y => not x DC y /\ not x EC y /\ not x PO y /\ not x NTPP y 
		/\ not x TPPi y /\ not x NTPPi y /\ not x EQ y  			%(disj_TPP)% 
     . x NTPP y => not x DC y /\ not x EC y /\ not x PO y /\ not x TPP y 
		/\ not x TPPi y /\ not x NTPPi y /\ not x EQ y  			%(disj_NTPP)% 
     . x TPPi y => not x DC y /\ not x EC y /\ not x PO y /\ not x TPP y 
		/\ not x NTPP y /\ not x NTPPi y /\ not x EQ y  			%(disj_TPPi)% 
     . x NTPPi y => not x DC y /\ not x EC y /\ not x PO y /\ not x TPP y 
		/\ not x NTPP y /\ not x TPPi y /\ not x EQ y  				%(disj_NTPPi)% 
     . x EQ y => not x DC y /\ not x EC y /\ not x PO y /\ not x TPP y 
		/\ not x NTPP y /\ not x TPPi y /\ not x NTPPi y  			%(disj_EQ)% 


     . x DC y /\ y DC  z => x ? z 							%(cmps_DCDC)%
     . x DC y /\ y EC  z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x NTPP z 		%(cmps_DCEC)%
     . x DC y /\ y PO z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x NTPP z 		%(cmps_DCPO)%
     . x DC y /\ y TPP z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x NTPP z 		%(cmps_DCTPP)%
     . x DC y /\ y NTPP z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x NTPP z		%(cmps_DCNTPP)%
     . x DC y /\ y TPPi z => x DC z 							%(cmps_DCTPPi)%
     . x DC y /\ y NTPPi z => x DC z							%(cmps_DCNTPPi)%

     . x EC y /\ y DC z => x DC z \/ x EC z \/ x PO z \/ x TPPi z \/ x NTPPi z 		%(cmps_ECDC)%
     . x EC y /\ y EC z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x TPPi z \/ x EQ z 	%(cmps_ECEC)%
     . x EC y /\ y PO z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x NTPP z		%(cmps_ECPO)%
     . x EC y /\ y TPP z => x EC z \/ x PO z \/ x TPP z \/ x NTPP z			%(cmps_ECTPP)%
     . x EC y /\ y NTPP z => x PO z \/ x TPP z \/ x NTPP z				%(cmps_ECNTPP)%
     . x EC y /\ y TPPi z => x DC z \/ x EC z 						%(cmps_ECTPPi)%
     . x EC y /\ y NTPPi z => x DC z							%(cmps_ECNTPPi)%

     . x PO y /\ y DC z => x DC z \/ x EC z \/ x PO z \/ x TPPi z \/ x NTPPi z		%(cmps_PODC)%
     . x PO y /\ y EC z => x DC z \/ x EC z \/ x PO z \/ x TPPi z \/ x NTPPi z		%(cmps_POEC)%
     . x PO y /\ y PO z => x ? z 							%(cmps_POPO)%
     . x PO y /\ y TPP z => x PO z \/ x TPP z \/ x NTPP z				%(cmps_POTPP)%
     . x PO y /\ y NTPP z => x PO z \/ x TPP z \/ x NTPP z				%(cmps_PONTPP)%
     . x PO y /\ y TPPi z => x DC z \/ x EC z \/ x PO z \/ x TPPi z \/ x NTPPi z	%(cmps_POTPPi)%
     . x PO y /\ y NTPPi z => x DC z \/ x EC z \/ x PO z \/ x TPPi z \/ x NTPPi	z 	%(cmps_PONTPPi)%

     . x TPP y /\ y DC z => x DC z							%(cmps_TPPDC)%
     . x TPP y /\ y EC z => x DC z \/ x EC z						%(cmps_TPPEC)%
     . x TPP y /\ y PO z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x NTPP z		%(cmps_TPPPO)%
     . x TPP y /\ y TPP z => x TPP z \/ x NTPP z					%(cmps_TPPTPP)%
     . x TPP y /\ y NTPP z => x NTPP z							%(cmps_TPPNTPP)%
     . x TPP y /\ y TPPi z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x TPPi z \/ x EQ	z %(cmps_TPPTPPi)%
     . x TPP y /\ y NTPPi z => x DC z \/ x EC z \/ x PO z \/ x TPPi z \/ x NTPPi z	%(cmps_TPPNTPPi)%

     . x NTPP y /\ y DC z => x DC z							%(cmps_NTPPDC)%
     . x NTPP y /\ y EC z => x DC z							%(cmps_NTPPEC)%
     . x NTPP y /\ y PO z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x NTPP z		%(cmps_NTPPPO)%
     . x NTPP y /\ y TPP z => x NTPP z							%(cmps_NTPPTPP)%
     . x NTPP y /\ y NTPP z => x NTPP z							%(cmps_NTPPNTPP)%
     . x NTPP y /\ y TPPi z => x DC z \/ x EC z \/ x PO z \/ x TPP z \/ x NTPP	z	%(cmps_NTPPTPPi)%
     . x NTPP y /\ y NTPPi z => x ? z							%(cmps_NTPPNTPPi)%

     . x TPPi y /\ y DC z => x DC z \/ x EC z \/ x PO z \/ x TPPi z \/ x NTPPi z	%(cmps_TPPiDC)%
     . x TPPi y /\ y EC z => x EC z \/ x PO z \/ x TPPi z \/ x NTPPi z			%(cmps_TPPiEC)%
     . x TPPi y /\ y PO z => x PO z \/ x TPPi z \/ x NTPPi z				%(cmps_TPPiPO)%
     . x TPPi y /\ y TPP z => x PO z \/ x EQ z \/ x TPP z \/ x TPPi z			%(cmps_TPPiTPP)%
     . x TPPi y /\ y NTPP z => x PO z \/ x TPP z \/ x NTPP z				%(cmps_TPPiNTPP)%
     . x TPPi y /\ y TPPi z => x TPPi z \/ x NTPPi z					%(cmps_TPPiTPPi)%
     . x TPPi y /\ y NTPPi z => x NTPPi	z						%(cmps_TPPiNTPPi)%

     . x NTPPi y /\ y DC z => x DC z \/ x EC z \/ x PO z \/ x TPPi z \/ x NTPPi	z	%(cmps_NTPPiDC)%
     . x NTPPi y /\ y EC z => x PO z \/ x TPPi z \/ x NTPPi z				%(cmps_NTPPiEC)%
     . x NTPPi y /\ y PO z => x PO z \/ x TPPi z \/ x NTPPi z				%(cmps_NTPPiPO)%
     . x NTPPi y /\ y TPP z => x PO z \/ x TPPi z \/ x NTPPi z				%(cmps_NTPPiTPP)%
     . x NTPPi y /\ y NTPP z => x PO z \/ x TPP z \/ x TPPi z \/ x NTPP z \/ x NTPPi z \/ x EQ	z %(cmps_NTPPiNTPP)%
     . x NTPPi y /\ y TPPi z => x NTPPi z						%(cmps_NTPPiTPPi)%
     . x NTPPi y /\ y NTPPi z => x NTPPi z						%(cmps_NTPPiNTPPi)%

end



logic HasCASL

view NN:
     { RCC_FO } to
     { ExtMetricSpaceByClosedDiscs[MetricSpace] 
	then %def
	pred __C__: ClosedDiscs * ClosedDiscs
	forall x,y:ClosedDiscs
	. x C y <=> not x disjoint y
	}	
=    QReg |-> ClosedDiscs
	





